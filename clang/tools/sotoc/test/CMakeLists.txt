
if(CMAKE_VERSION VERSION_LESS 3.1.20141117)
  set(cmake_3_2_USES_TERMINAL)
else()
  set(cmake_3_2_USES_TERMINAL USES_TERMINAL)
endif()


set(SOTOC_TEST_COMPILER ${CMAKE_BINARY_DIR}/bin/clang CACHE STRING
    "Compiler to use for testing sotoc")

# We assume libomp is available in the lib dir
set(SOTOC_LIBOMP_LIBRARY_DIR ${CMAKE_BINARY_DIR}/lib)
set(SOTOC_LIBOMPT_LIBRARY_DIR ${CMAKE_BINARY_DIR}/projects/openmp/libomptarget)
set(SOTOC_OMP_HEADER_DIR ${CMAKE_BINARY_DIR}/projects/openmp/runtime/src)


find_package(Python3 REQUIRED)

if(NOT Python3_FOUND)
  message(WARNING "Could not find Python.")
  message(WARNING "The check-sotoc target will not be available!")
  return()
endif()


set(SOTOC_LLVM_LIT_EXE
    ${LLVM_TOOLS_BINARY_DIR}/llvm-lit${CMAKE_EXECUTABLE_SUFFIX}
    CACHE
    FILEPATH
    "Path to the llvm-lit utility. Should be build together with the other LLVM utilities")

set(SOTOC_LIT_ARGS "-v;--no-progress-bar;-j 1")


set(SOTOC_FILECHECK_EXE ${LLVM_TOOLS_BINARY_DIR}/FileCheck${CMAKE_EXECUTABLE_SUFFIX} CACHE FILEPATH "Path to FileCheck utility. Should be built together with other LLVM utilities")

add_custom_target(check-sotoc
  COMMAND ${Python3_EXECUTABLE} ${SOTOC_LLVM_LIT_EXE} ${SOTOC_LIT_ARGS} ${CMAKE_CURRENT_BINARY_DIR}
    DEPENDS sotoc
    COMMENT "Running sotoc tests"
    ${cmake_3_2_USES_TERMINAL}
  )


# Configure the lit.site.cfg.in file
set(SOTOC_EXECUTABLE ${CMAKE_BINARY_DIR}/bin/sotoc)
set(SOTOC_TEST_DIR ${CMAKE_CURRENT_SOURCE_DIR})
set(SOTOC_OBJ_ROOT ${CMAKE_CURRENT_BINARY_DIR})
set(AUTO_GEN_COMMENT "## Autogenerated by sotoc configuration.\n# Do not edit!")
configure_file(lit.site.cfg.in lit.site.cfg @ONLY)

